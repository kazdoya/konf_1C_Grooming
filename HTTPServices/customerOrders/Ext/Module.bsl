
Функция createOrderPOST(Запрос)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Запрос.ПолучитьТелоКакСтроку()); //1
	ДанныеЗапроса = ПрочитатьJSON(ЧтениеJSON, Ложь);
	ЧтениеJSON.Закрыть();
	
	НоваяЗапись = Документы.ЗаписьКлиента.СоздатьДокумент(); //2
	НоваяЗапись.Сотрудник = Справочники.Сотрудники.НайтиПоНаименованию(ДанныеЗапроса.Сотрудник);
	НоваяЗапись.Дата = ТекущаяДата();
	НоваяЗапись.ДатаЗаписи = ПрочитатьДатуJSON(ДанныеЗапроса.ДатаЗаписи, ФорматДатыJSON.ISO);//3
	НоваяЗапись.Клиент = Справочники.Пользователи.НайтиПоКоду(ДанныеЗапроса.GUIDПользователя).Клиент;
	
	Для Каждого СтруктураУслуги Из ДанныеЗапроса.МассивУслуг Цикл //4
		НоваяУслуга = НоваяЗапись.ОписаниеУслуг.Добавить();
		НоваяУслуга.Услуга  = Справочники.Номенклатура.НайтиПоНаименованию(СтруктураУслуги.Услуга);
		НоваяУслуга.Стоимость = Число(СтруктураУслуги.Цена);
		НоваяУслуга.Количество = Число(СтруктураУслуги.Количество);
		НоваяУслуга.Сумма = Число(СтруктураУслуги.Сумма);
	КонецЦикла;
	
	Попытка
		НоваяЗапись.Записать(РежимЗаписиДокумента.Проведение); //5
		ТекстОтвета = "Создан и проведен документ " + НоваяЗапись.Номер;
		Ответ = Новый HTTPСервисОтвет(200);
	Исключение
		Ответ = Новый HTTPСервисОтвет(500);
		ТекстОтвета = "Произошла ошибка при проведении документа " + НоваяЗапись.Номер + "! ";
	КонецПопытки;
	
	Ответ.УстановитьТелоИзСтроки(ТекстОтвета);
	
	Возврат Ответ;
	
КонецФункции